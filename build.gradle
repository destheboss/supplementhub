plugins {
    id 'jacoco'
    id "org.sonarqube" version "5.1.0.4882"
}

subprojects {
    apply plugin: "jacoco"

    tasks.matching { it.name == "sonar" }.configureEach {
        enabled = false
    }

    plugins.withId("java") {
        tasks.named("test") {
            useJUnitPlatform()
            finalizedBy("jacocoTestReport")
        }

        tasks.named("jacocoTestReport") {
            dependsOn("test")
            reports {
                xml.required = true
                html.required = true
            }
        }

        repositories { mavenCentral() }
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectName", "SupplementHub"
        property "sonar.organization", "destheboss"
        property "sonar.projectKey", "destheboss_Supplementhub"
        property "sonar.coverage.jacoco.xmlReportPaths", "**/build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**/build/**,**/.gradle/**,**/.idea/**,**/out/**,**/.github/**"
    }
}
